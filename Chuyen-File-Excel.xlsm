' Author: Nam-Nguyen
' Date: 03-12-2025
' Huong dan:
' Nhấn tổ hợp phím Alt + F11 (để mở cửa sổ Microsoft Visual Basic).
' Trên thanh menu, chọn Insert > Module.

Sub ChuyenDoiSangThuMucRieng()
    Dim strSourcePath As String
    Dim strDestPath As String
    Dim strFile As String
    Dim wbk As Workbook
    
    ' --- BUOC 1: Chon Thu Muc Noi Luu Cu (.xls) ---
    With Application.FileDialog(msoFileDialogFolderPicker)
        .Title = "BUOC 1: Chon thu muc chua file CU (.xls)"
        .Show
        If .SelectedItems.Count = 0 Then Exit Sub
        strSourcePath = .SelectedItems(1) & "\"
    End With
    
    ' --- BUOC 2: Chon Thu Muc Noi Luu Moi (.xlsx) ---
    With Application.FileDialog(msoFileDialogFolderPicker)
        .Title = "BUOC 2: Chon thu muc de luu file MOI (.xlsx)"
        .Show
        If .SelectedItems.Count = 0 Then Exit Sub
        strDestPath = .SelectedItems(1) & "\"
    End With

    ' Tat man hình ch?p nháy và các c?nh báo Compatibility
    Application.ScreenUpdating = False
    Application.DisplayAlerts = False

    strFile = Dir(strSourcePath & "*.xls")

    ' Bat dau xu ly
    Do While strFile <> ""
        ' Kiem tra xu ly cac file 
        If Right(strFile, 4) = ".xls" Then
            
            ' Mo file thu muc nguon
            Set wbk = Workbooks.Open(Filename:=strSourcePath & strFile)
            
            ' Luu file sang thu m?c Ðích v?i d?nh d?ng m?i
            ' Tên file m?i s? là Tên_Cu + "x" (ví d?: data.xls -> data.xlsx)
            wbk.SaveAs Filename:=strDestPath & strFile & "x", FileFormat:=xlOpenXMLWorkbook
            
            ' Ðóng file l?i
            wbk.Close SaveChanges:=False
        End If
        strFile = Dir
    Loop

    ' B?t l?i các tính nang h? th?ng
    Application.ScreenUpdating = True
    Application.DisplayAlerts = True

    MsgBox "Xong! File moi da duoc luu tai: " & vbCrLf & strDestPath, vbInformation
End Sub
